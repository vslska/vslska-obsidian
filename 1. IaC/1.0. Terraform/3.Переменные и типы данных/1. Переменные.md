Каждая переменная должна быть объявлена с помощью специального блока `variable`:

```
variable "image_id" {
  type = string
}

variable "security_group_ids" {
  description = "(Optional) - List of security group IDs."
  type        = list(string)
  default     = []
}
``````

После ключевого слова `variable` идёт уникальное в рамках манифеста название переменной. Это имя используется для указания значения переменной и обращения к ней в коде.

> [!Важно] Важно
> Нельзя  назначить переменной зарезервированные имена: **source, version, providers, count, for_each, lifecycle, depends_on, locals**.

### Аргументы  для объявления переменных
##### **Аргумент `description`**
Служит для описания переменной в документации.

Описание `description` должно кратко объяснять цель переменной и ожидаемый тип значения. Эта строка может быть включена в документацию о модуле, поэтому она должна быть написана с точки зрения пользователя модуля, а не его автора. Также можно включить информацию о том, обязательно ли указывать переменную.

Пример написания:
```
variable "security_groups_ids" {
  description = "(Optional) - List of security group IDs."
  type        = list(string)
  default     = []
}
```

Из примера видно, что переменная необязательная, от пользователя ожидается список идентификаторов Security Group.

#### **Аргумент `type`**

Определяет, какой тип данных принимается для переменной. Например, **string** для строкового значения или **number** — для числового.

Пример написания:

```
variable "pg_version" {
  description = "PostgreSQL version"
  type        = number
  default     = 15
}
```

Тип данных переменной — **string**.

#### **Аргумент `default`**

Значение переменной по умолчанию, которое делает её необязательной и будет использовано, если не дать значение переменной.

Пример написания:
```
variable "pg_version" {
  description = "PostgreSQL version"
  type        = string
  default     = "15"
}
```

Переменная получит значение **"15"**, если пользователь не задаст её значение.
#### **Аргумент `validation`**

Блок для определения правил проверки, обычно в дополнение к ограничениям типа данных.

Пример написания:
```
variable "pg_version" {
  description = "PostgreSQL version"
  type        = string
  default     = "15"
  validation {
    condition     = contains(["12", "12-1c", "13", "13-1c", "14", "14-1c", "15", "15-1c", "16"], var.pg_version)
    error_message = "Allowed PostgreSQL versions are 12, 12-1c, 13, 13-1c, 14, 14-1c, 15, 15-1c, 16."
  }
}
```
Разрешены только определённые значения переменной **pg_version**, соответствующие доступным версиям PostgreSQL. При использовании иного значения будет сообщение об ошибке, указанное в аргументе `error_message`.

#### **Аргумент `sensitive`**

Ограничивает вывод интерфейса Terraform при использовании переменной в конфигурации. Объявление переменной с аргументом `sensitive = true` заменит её значение в выводе команд `terraform plan` и `terraform apply` на `(sensitive value)`.
```
variable "user_information" {
  type = object({
    name    = string
    address = string
  })
  sensitive = true
}

resource "some_resource" "a" {
  name    = var.user_information.name
  address = var.user_information.address
}

Terraform will perform the following actions:

  # some_resource.a will be created
  + resource "some_resource" "a" {
      + name    = (sensitive value)
      + address = (sensitive value)
    }

Plan: 1 to add, 0 to change, 0 to destroy.
```

#### **Аргумент `nullable`**

Контролирует, может ли переменной быть присвоено значение **null**. По умолчанию аргументу присвоено значение **true**.

Пример написания:
```
variable "example" {
  type     = string
  nullable = false
}
```
У переменной не задано значение аргумента `default` и указано `nullable = false`. Таким образом, это обязательная переменная, которой необходимо указать некоторое значение, отличное от **null**.
