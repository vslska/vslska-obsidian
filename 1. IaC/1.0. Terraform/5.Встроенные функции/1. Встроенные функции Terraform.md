Язык написания конфигурационных файлов HashiCorp Configuration Language (HCL) Terraform включает встроенные функции, которые можно вызывать из выражений для преобразования и комбинирования значений. Общий синтаксис вызова функций — это имя функции, за которым следуют аргументы в круглых скобках, разделённые запятыми:
```
max(2, 4, 18)
```
Terraform не поддерживает пользовательские функции. Вам доступны только встроенные функции, написанные разработчиками Terraform, их достаточно много.
```
variable "value" {
  default = "hello"
}

output "transformed_value" {
  value = upper(format(“transformed: %s”, var.value))
}
```
В этом примере используются две функции:
- `format` — создаёт строку, форматируя значения;
- `upper` — преобразует строку в верхний регистр.
Таким образом, значение переменной `value` ("hello") сначала объединяется с префиксом "transformed: ", а затем преобразуется в верхний регистр.

_Встроенные функции Terraform_
![[Pasted image 20251003213728.png]]
#### Работа с числами
Функции используются для определения минимального (`min`) или максимального (`max`) значения из набора чисел или для округления — `floor`, `ceil`.
Пример:
```
timeout = min(var.timeout, 30)
```
Задано значение тайм-аута не выше 30.
### Работа со строками
Функции используются для преобразований строковых значений. Например, с помощью `replace` можно заменить подстроку, с помощью `substr` — получить часть строки, а благодаря функции `format` — подставлять в строку другие значения.
Пример:
```
domain_name = replace(data.yandex_api_gateway.this.domain, "https://", "")
```
Часть строки, содержащая https://, будет отброшена (заменена на пустую строку).
### Работа с коллекциями
Функции `flatten`, `length`, `map`, `list`, `sort` могут использоваться для обработки и преобразования коллекций данных. Функция `flatten` принимает список со вложенными списками и возвращает плоский список.
Пример:
```
variable "cidr_blocks" {
  type = map(list(string))
  default = {
    ru-central1-a = ["192.168.1.0/24", "192.168.2.0/24", "192.168.3.0/24"],
    ru-central1-b = ["192.168.4.0/24", "192.168.5.0/24"],
    ru-central1-d = ["192.168.6.0/24", "192.168.7.0/24", "192.168.8.0/24", "192.168.9.0/24"],
  }
}

output "all_cidr_blocks" {
  value = flatten(values(var.cidr_blocks))
}

```

Функция `flatten` вернёт список в следующем виде:
```
    + all_cidr_blocks = [
      + "192.168.1.0/24",
      + "192.168.2.0/24",
      + "192.168.3.0/24",
      + "192.168.4.0/24",
      + "192.168.5.0/24",
      + "192.168.6.0/24",
      + "192.168.7.0/24",
      + "192.168.8.0/24",
      + "192.168.9.0/24",
    ]
```
### Кодирование и декодирование
Функции выполняют кодирование, декодирование и преобразование в форматы **base64, JSON, YAML** и другие.
Пример:
```
locals {
  encoded_secret_key = base64encode(var.secret_key)
}
```
Функция `base64encode` используется для кодирования значения в формат **base64**.
### Операции с файловой системой
Функции проверяют существование файла с помощью `fileexists`, читают его содержимое с помощью `file` и создают конфигурационный файл на основе шаблона с помощью `templatefile`.
Пример на основе шаблона `backend.tftpl`:
```
url = "${url}"
secret_key = "${secret_key}" 
```

```
output "backend_config" {
  value = templatefile("backend.tftpl", {
    url        = "https://example.com:8000",
    secret_key = "3njfui34fw3f3qh93"})

}
```
Можно получить конфигурационный файл с переданными значениями:
```
  backend_config = <<-EOT
        url = "https://example.com:8000"
        secret_key = "3njfui34fw3f3qh93"
    EOT
```
### Работа с датой и временем
Функции позволяют работать с датой и временем. Например, можно получить UTC Timestamp с помощью `timestamp`, конвертировать его в различные форматы с помощью `formatdate`.
Пример:
```
locals {
  datetime   = formatdate("YYYY-MM-DD.hh-mm-ss", timestamp())
  object_key = "${var.project_name}_${local.datetime}.zip"
}

resource "yandex_storage_object" "cute-cat-picture" {
...
  key = local.object_key
...
}
```
В этом примере формируется имя файла для последующего создания объекта в Yandex Object Storage.
### Шифрование и хеш-функции
Функции хеширования и криптографических операций в Terraform позволяют выполнять хеширование данных и генерацию криптографических ключей и подписей для обеспечения безопасности конфигураций и ресурсов.
Пример:
```
resource "yandex_storage_object" "cute-cat-picture" {
...
  source_hash = filemd5("./object.zip")
...
}
```
Функция `filemd5` вызывает обновление объекта, когда поменялось его содержимое.
### Работы с сетевой CIDR
Функции позволяют работать с сетевыми CIDR-блоками. Например, функция `cidrhost` калькулирует адрес хоста, функция `cidrnetmask` возвращает сетевую маску.
```
variable "subnet_a_vpc_2" {
  default = "10.161.1.0/24"
}

resource "yandex_compute_instance" "this" {
  network_interface {
...
    ip_address = "${cidrhost(var.subnet_a_vpc_2, 10)}"
...
  }
}
```
Функция `cidrhost` используется для получения IP-адреса хоста с указанным индексом в заданной подсети. В данном случае она используется для получения IP-адреса десятого хоста в подсети subnet_a_vpc_2.
### Конвертирование типов данных
Функции позволяют выполнять преобразования различных типов данных. Например, функция `tonumber` конвертирует значение в тип данных **number**, `try` — возвращает первое валидное значение.
Пример:
```
resource "yandex_kms_symmetric_key" "this" {
  ...
  name  = try(coalesce(var.sse_kms_key_configuration.name, local.sse_kms_master_key_name), null)
  ...
}  
```
Этот код использует функцию `coalesce`, чтобы выбрать либо значение переменной `var.sse_kms_key_configuration.name`, либо локальную переменную `local.sse_kms_master_key_name`, и возвращает null с помощью функции `try`, если оба значения отсутствуют.
## Как проверить функцию
Можно экспериментировать со встроенными функциями Terraform из консоли, запустив команду `terraform console`:
```
$ terraform console

> max(2, 4, 18)
18

> cidrnetmask("172.16.0.0/12")
"255.240.0.0"
```
Команда `terraform console` считывает конфигурацию Terraform из текущего рабочего каталога и файла state — состояния конфигурации.

> [!NOTE] Важно
> Перед использованием `terraform console` необходимо инициализировать директорию командой `terraform init`. Для обращения к атрибутам ресурсов из консоли необходимо наличие state: инфраструктура должны быть применена, иначе атрибуты будут указаны как `(known after apply)`.

Это позволяет оперировать переменными и значениями, определёнными в конфигурации, например содержимым файла `variables.tf`:
```
variable "image_id" {
  description = "(Optional) - Boot disk image id. If not provided, it defaults to Ubuntu 22.04 LTS image id"
  type        = string
  default     = "fd833v6c5tb0udvk4jo6"
}
```
Можно использовать значение переменной `image_id` при тестировании:
```
$ terraform console

> upper(var.image_id)
"FD833V6C5TB0UDVK4JO6"
```
Команда `terraform console` может использоваться в неинтерактивных сценариях путём перенаправления через пайп строковых команд. Выводится только результат выполнения последней команды (если ранее не было ошибки).
```
$ echo 'ceil(0.79)' | terraform console
1
```

> [!NOTE] Запомни
> - Terraform не поддерживает пользовательские функции, вы можете использовать только встроенные.
> - Встроенные функции закрывают базовые потребности пользователей и комбинируются в выражения.
> - Встроенные функции Terraform делятся на девять типов по назначению: работа с числами, работа со строками, работа с коллекциями, кодирование и декодирование, операции с файловой системой, работа с датой и временем, шифрование и хеш-функции, работы с сетевой CIDR, конвертирование типов данных.

