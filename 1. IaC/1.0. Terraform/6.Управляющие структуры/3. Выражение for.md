Выражение `for` создаёт значение сложного типа данных, преобразуя другое значение сложного типа данных.
```
[for s in var.list : upper(s)]
```
Это выражение обрабатывает каждый элемент переменной `list` и рассчитывает выражение с использованием функции `upper(s)`. В результате получится `tuple(string)` со значениями, преобразованными в верхний регистр.

Выражение `for` может обрабатывать такие типы данных, как `list`, `set`, `tuple`, `map`, `object`.

Выражение в примере выше использовало только одно временное значение `s`, но можно обрабатывать и два:
```
[for k, v in var.map : length(k) + length(v)]
```
Для типов данных `map` и `object` временное значение `k` — ключ или название атрибута объекта, `v` — значение.

Например, следующее выражение вернёт список строк c количеством серверов:
```
variable "servers_count" {
  default = {
    "db" = 3,
    "frontend" = 2,
    "backend" = 5,
    "balancer" = 1
  }
}

output "servers" {
  value = [for k, v in var.servers_count: 
    "${k} has ${v} servers"
  ]
}
```

```
Outputs:

servers = [
  "backend has 5 servers",
  "balancer has 1 servers",
  "db has 3 servers",
  "frontend has 2 servers",
]
```

При работе со списками также можно использовать два значения. В этом случае первое значение будет содержать индекс элемента, начинающийся с `0`, а второе будет содержать значение:
```
[for i, v in var.list : "${i} is ${v}"]
```
Вместо символов `k`, `i`, `v` можно использовать любые другие символы или слова, позволяющие лучше идентифицировать значения для пользователя.

### Тип данных результата

Скобки вокруг выражения `for` определяют, каким будет тип результирующего объекта:

- квадратные скобки `[ ]` — `tuple`;
- фигурные скобки `{ }` — `object`.

При создании объекта следует использовать синтаксис, содержащий символ `=>`:
```
{for s in var.list : s => upper(s)}
```
Результатом указанного выражения станет:
```
{
  aaa = "AAA"
  bbb = "BBB"
  ccc = "CCC"
}
```
### Фильтрация значений
В выражении `for` можно использовать фильтрацию с помощью опционального выражения `if`:
```
[for s in var.list : upper(s) if s != ""]
```
