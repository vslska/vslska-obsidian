В большинстве случаев Terraform сам управляет зависимостями. Вам нужно явно указать зависимость только в том случае, когда ресурс или модуль зависит от поведения другого ресурса, но не обращается к данным этого ресурса в своих аргументах.
Для указания явной зависимости используется аргумент `depends_on`. Он указывает Terraform завершить все действия с объектом зависимости перед выполнением действий с объектом, объявляющим зависимость.

В аргументе `depends_on` указывается список ссылок на другие ресурсы.

В примере ниже создаются базы данных и пользователи Yandex Managed Service for MySQL. В ресурсе `yandex_mdb_mysql_user` нет прямой ссылки на ресурс `yandex_mdb_mysql_database`, аргументы задаются переменными. В этом случае Terraform не сможет сам управлять зависимостью, её необходимо указать явно. С такой явно указанной зависимостью пользователи будут созданы после баз данных.
```
variable "databases" {
...
  type = list(object({
    name = string
  }))
...
}

variable "users" {
...
  type = list(object({
    name                  = string
    password              = optional(string, null)
    authentication_plugin = optional(string, null)
    global_permissions    = optional(list(string), [])
    connection_limits = optional(object({
      max_questions_per_hour   = optional(number, -1)
      max_updates_per_hour     = optional(number, -1)
      max_connections_per_hour = optional(number, -1)
      max_user_connections     = optional(number, -1)
    }), null)
    permissions = optional(list(object({
      database_name = string
      roles         = optional(list(string), ["ALL"])
    })), [])
  }))
...
}

resource "yandex_mdb_mysql_database" "database" {
  for_each = length(var.databases) > 0 ? { for db in var.databases : db.name => db } : {}

  cluster_id = yandex_mdb_mysql_cluster.this.id
  name       = lookup(each.value, "name", null)
}

resource "yandex_mdb_mysql_user" "user" {
...

  dynamic "permission" {
    for_each = lookup(each.value, "permissions", [])
    content {
      database_name = permission.value.database_name
      roles         = permission.value.roles
    }
  }
...
  # Create databases before users 
  depends_on = [
    yandex_mdb_mysql_database.database
  ]
}
```
Рекомендуется всегда добавлять комментарий, объясняющий причины явной зависимости.
Вместо `depends_on` предпочтительно использовать выражения со ссылкой на ресурсы для указания зависимости. Эти выражения позволяют Terraform понимать, на какой ресурс ведёт ссылка, и избегать планирования изменений, если этот конкретный элемент значения не изменился, даже когда запланированы изменения других частей объекта верхнего уровня.

> [!NOTE] Важно
> Используйте `depends_on` в крайнем случае: с ним Terraform может усложнять планы применения изменений и заменять больше ресурсов, чем необходимо.

> [!NOTE] Важно
> Вместо `depends_on` рекомендуется использовать выражения, ссылающиеся на ресурсы, чтобы определять зависимости. Эти выражения позволяют Terraform понимать, на какой ресурс ведёт ссылка, и избегать планирования изменений, если конкретный элемент значения не изменился, даже когда запланированы изменения других частей объекта верхнего уровня.
> 
> Если вам всё же потребовалось указать явную зависимость между ресурсами, проверьте ваш код. В подавляющем большинстве случаев указания явной зависимости можно и следует избегать, используя выражения со ссылками на ресурсы.
